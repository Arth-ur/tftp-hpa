dnl
dnl autoconf input file to generate MCONFIG
dnl

AC_INIT(MCONFIG.in)
AC_PREFIX_DEFAULT(/usr)

AC_PROG_CC
AC_C_CONST
AC_C_INLINE

AC_CHECK_HEADERS(sysexits.h)
AC_CHECK_HEADERS(strings.h)
AC_CHECK_HEADERS(libgen.h)
AC_CHECK_HEADERS(sys/filio.h)

AC_SEARCH_LIBS(socket, socket, , [AC_MSG_ERROR(socket library not found)])
AC_SEARCH_LIBS(gethostbyname, [nsl resolv], , [AC_MSG_ERROR(gethostbyname not found)])
AC_SEARCH_LIBS(inet_aton, [nsl resolv], , [AC_MSG_ERROR(inet_aton not found)])
AC_SEARCH_LIBS(herror, [nsl resolv], , [AC_MSG_ERROR(herror not found)])

AC_CHECK_FUNCS(setsid)
AC_CHECK_FUNCS(recvmsg)
AC_CHECK_FUNCS(setreuid)
AC_CHECK_FUNCS(setregid)
PA_MSGHDR_MSG_CONTROL
PA_STRUCT_IN_PKTINFO

PA_WITH_BOOL(tcpwrappers, 1,
[ --without-tcpwrappers	Disable tcpwrapper permissions checking],
[
	AC_SEARCH_LIBS(yp_get_default_domain, [nsl resolv])
	PA_HAVE_TCPWRAPPERS
],:)

PA_WITH_BOOL(remap, 1,
[ --without-remap       Disable regex-based filename remapping],
[
	AC_CHECK_HEADER(regex.h,
	[
		AC_SEARCH_LIBS(regcomp, [regex rx],
		[
			AC_DEFINE(WITH_REGEX)
			TFTPDOBJS="remap.o $TFTPDOBJS"
		])
	])
],:)

PA_ADD_CFLAGS(-Wall)
PA_ADD_CFLAGS(-W)
PA_ADD_CFLAGS(-Wpointer-arith)
PA_ADD_CFLAGS(-Wbad-function-cast)
PA_ADD_CFLAGS(-Wcast-equal)
PA_ADD_CFLAGS(-Wstrict-prototypes)
PA_ADD_CFLAGS(-Wmissing-prototypes)
PA_ADD_CFLAGS(-Wmissing-declarations)
PA_ADD_CFLAGS(-Wnested-externs)
PA_ADD_CFLAGS(-Winline)
PA_ADD_CFLAGS(-Wcast-align)
PA_ADD_CFLAGS(-pipe)

PA_SIGSETJMP([AC_DEFINE(HAVE_SIGSETJMP)])

LIBXTRA=false
AC_SEARCH_LIBS(xmalloc, iberty, , LIBXTRA=true LIBOBJS="$LIBOBJS xmalloc.o")
AC_SEARCH_LIBS(xstrdup, iberty, , LIBXTRA=true LIBOBJS="$LIBOBJS xstrdup.o")
AC_SEARCH_LIBS(bsd_signal, bsd, , LIBXTRA=true LIBOBJS="$LIBOBJS bsdsignal.o")
if $LIBXTRA; then
	LIBS="../lib/libxtra.a $LIBS"
fi

AC_SUBST(LIBOBJS)
AC_SUBST(TFTPDOBJS)

AC_PROG_RANLIB
AC_PROG_INSTALL

AC_CONFIG_HEADER(config.h)
AC_OUTPUT(MCONFIG)
